plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'androidx.navigation.safeargs.kotlin'
}

def appVersionCode
if (project.hasProperty("CommitCount")) {
    appVersionCode = Integer.parseInt(project.getProperties().get("CommitCount"))
} else {
    appVersionCode = rootProject.ext.gitCount().toInteger()
}

android {
    compileSdkVersion androidCompileSdkVersion
    buildToolsVersion androidBuildToolsVersion

    signingConfigs {
        release keystore.signingConfigs
        debug keystore.debugSigningConfigs
    }

    defaultConfig {
        applicationId "com.inhealion.generator"
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion
        versionCode appVersionCode
        versionName appVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "BASE_URL", "\"https://inhealion.gr/device/\""
    }

    buildFeatures {
        viewBinding true
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            debuggable true
            manifestPlaceholders = [crashlyticsCollectionEnabled: "false"]
        }

        release {
            debuggable false
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [crashlyticsCollectionEnabled: "true"]
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }
}

dependencies {
    implementation project(":service")
    implementation project(":data")

    implementation deps.androidx.core
    implementation deps.androidx.preference
    implementation deps.androidx.fragment
    implementation deps.androidx.lifecycle_livedata

    implementation deps.androidx.nav.fragment
    implementation deps.androidx.nav.ui
    implementation deps.androidx.nav.dynamic_features_fragment

    implementation deps.androidx.appcompat
    implementation deps.androidx.swiperefreshlayout
    implementation deps.androidx.constraintlayout

    implementation platform(deps.google.firebase.bom)
    implementation deps.google.firebase.crashlytics
    implementation deps.google.firebase.analytics
    implementation deps.google.material

    implementation deps.koin
    implementation deps.timber
    implementation deps.dexter
    implementation deps.joda

    testImplementation deps.test.junit
    androidTestImplementation deps.test.androidx.junit
    androidTestImplementation deps.test.androidx.espresso
    androidTestImplementation deps.test.androidx.nav
}
