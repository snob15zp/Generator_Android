plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'androidx.navigation.safeargs'
}

apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.gms.google-services'

def appVersionCode
if (project.hasProperty("CommitCount")) {
    appVersionCode = Integer.parseInt(project.getProperties().get("CommitCount"))
} else {
    appVersionCode = rootProject.ext.gitCount().toInteger()
}

def appVersionName = "1.1.0"

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.2"

    signingConfigs {
        release keystore.signingConfigs
        debug keystore.debugSigningConfigs
    }

    defaultConfig {
        applicationId "com.inhealion.generator"
        minSdkVersion 23
        targetSdkVersion 31
        versionCode appVersionCode
        versionName appVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "BASE_URL", "\"https://inhealion.gr/device/\""
    }

    buildFeatures {
        viewBinding true
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            debuggable true
            manifestPlaceholders = [crashlyticsCollectionEnabled: "false"]
        }

        release {
            debuggable true
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [crashlyticsCollectionEnabled: "true"]
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }
}

dependencies {
    implementation project(":service")
    implementation project(":data")

    implementation 'com.karumi:dexter:6.2.2'

    implementation("androidx.core:core-ktx:$kotlin_ktx_version")
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version")

    implementation "androidx.preference:preference-ktx:$preference_version"
    implementation "androidx.fragment:fragment-ktx:1.3.6"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

    implementation "io.insert-koin:koin-android:$koin_version"

    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.navigation:navigation-dynamic-features-fragment:$nav_version"
    implementation "androidx.navigation:navigation-compose:2.4.0-alpha10"

    implementation platform('com.google.firebase:firebase-bom:26.8.0')
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'

    implementation "com.jakewharton.timber:timber:$timber_version"

    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'

    implementation 'net.danlew:android.joda:2.10.9.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.navigation:navigation-testing:$nav_version"
}
